apply plugin: 'info.solidsoft.pitest.aggregator'

allprojects {
    group = 'org.nequi'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'info.solidsoft.pitest'
    apply plugin: 'java-library'

    java {
        sourceCompatibility = JavaVersion.VERSION_18
        targetCompatibility = JavaVersion.VERSION_18
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }

    dependencies {
        implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        implementation platform("software.amazon.awssdk:bom:2.31.25")

        implementation 'io.projectreactor:reactor-core'
        implementation 'io.projectreactor.addons:reactor-extra'

        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.projectreactor:reactor-test'
        testImplementation 'io.projectreactor.tools:blockhound-junit-platform:1.0.11.RELEASE'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        if(!project.name.equalsIgnoreCase('model') && !project.name.equalsIgnoreCase('usecase')){
            dependencies.add("implementation", "ch.qos.logback:logback-classic:${logbackVersion}")
            dependencies.add("implementation", "net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
        }

    }

    pitest {
        pitestVersion ='1.19.1'
        junit5PluginVersion = '1.2.1'
        mutationThreshold = 80
        verbose = false
    }


}
tasks.named('wrapper') {
    gradleVersion = '8.14'
}